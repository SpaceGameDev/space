plugins {
	id 'space-plugin-mod'
	id 'java-library'
	id 'application'
	id 'glslang-validator'
}

group 'space.game'
version '1.0'

targetCompatibility = 1.11
sourceCompatibility = 1.11

mainClassName = "space.game.asteroidsDemo.AsteroidsDemo"

repositories {
	mavenLocal()
	mavenCentral()
}

glsl {
	compiler = "glslc"
}

sourceSets {
	main {
		glsl {
			srcDirs(java.sourceDirectories)
		}
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
}

dependencies {
	api platform('space.engine:space-engine:1.0')

	api 'space.engine:utils'
	implementation 'space.engine:vulkan'
	implementation 'space.engine:window-glfw'
	implementation 'space.engine:vulkan-glfw'

	api "org.lwjgl:lwjgl-assimp"
}

task extractRuntimeClasspath(type: Sync) {
	from { configurations.runtimeClasspath.collect { it.isFile() ? zipTree(it) : it } }
	into { buildDir.path + "/extractRuntimeClasspath" }
	dependsOn configurations.runtimeClasspath
}








@CacheableRule
class LwjglRule implements ComponentMetadataRule {

	def nativeVariants = [
			"natives-linux",
			"natives-windows",
			"natives-macos"
	]

	def nativeOverrides = [
			"lwjgl-vulkan": ["natives-macos"],
			"lwjgl-bom"   : []
	]

	void execute(ComponentMetadataContext context) {
		if (context.details.id.group == 'org.lwjgl') {
			context.details.allVariants {
				it.withFiles {
					def override = nativeOverrides.get(context.details.id.module.name)
					(override == null ? nativeVariants : override.findAll { nativeVariants.contains(it) }).each {
						addFile("${context.details.id.name}-${context.details.id.version}-${it}.jar")
					}
				}
			}
		}
	}
}

dependencies {
	components {
		all(LwjglRule)
	}
}
